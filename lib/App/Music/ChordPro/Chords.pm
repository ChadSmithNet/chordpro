#! perl

package App::Music::ChordPro::Chords;

use strict;
use warnings;
use utf8;

use constant CHORD_SONG => 1;

# Chords info, as a hash by chord name.
my %chords;
# Chord names, in the order of the list above.
my @chordnames;
# Chord order ordinals, for sorting.
my %chordorderkey; {
    my $ord = 0;
    for ( split( ' ', "C C# Db D D# Eb E F F# Gb G G# Ab A A# Bb B" ) ) {
	$chordorderkey{$_} = $ord;
	$ord += 2;
    }
}

# Additional chords, defined by the configs.
my %config_chords;

# Additional chords, defined by the user.
my %song_chords;

# Current tuning.
my @tuning;

# Transfer the info from the raw list into %chords and @chordnames.
my $chords_filled;
sub fill_tables {
    die("FATAL: No instrument?") unless @tuning;
}

# Returns a list of all chord names in a nice order.
sub chordcompare($$);
sub chordnames {
    fill_tables();
    [ sort chordcompare @chordnames ];
}

# Returns info about an individual chord.
sub chord_info {
    my ( $chord ) = @_;
    my $info;
    fill_tables();

    for ( \%song_chords, \%config_chords, \%chords ) {
	next unless exists($_->{$chord});
	$info = $_->{$chord};
	last;
    }

    my $s = strings();
    if ( ! $info && $::config->{diagrams}->{auto} ) {
	$info = [ CHORD_SONG, -1, (0) x $s ];
    }

    return unless $info;
    if ( $info->[1] <= 0 ) {
	return +{
		 name    => $chord,
		 strings => [ ],
		 base    => 1,
		 system  => "",
		 user    => 1,
		 };
    }
    return +{
	     name    => $chord,
	     strings => [ @{$info}[2..$s+1] ],
	     @$info > $s+2 ? ( fingers => [ @{$info}[$s+2..2*$s+1] ] ) : (),
	     base    => $info->[1],
	     system  => "",
	     user    => $info->[0],
    };
}

sub chordcompare($$) {
    my ( $chorda, $chordb ) = @_;
    my ( $a0, $arest ) = $chorda =~ /^([A-G][b#]?)(.*)/;
    my ( $b0, $brest ) = $chordb =~ /^([A-G][b#]?)(.*)/;
    $a0 = $chordorderkey{$a0}//return 0;
    $b0 = $chordorderkey{$b0}//return 0;
    return $a0 <=> $b0 if $a0 != $b0;
    $a0++ if $arest =~ /^m(?:in)?(?!aj)/;
    $b0++ if $brest =~ /^m(?:in)?(?!aj)/;
    for ( $arest, $brest ) {
	s/11/:/;		# sort 11 after 9
	s/13/;/;		# sort 13 after 11
	s/\((.*?)\)/$1/g;	# ignore parens
	s/\+/aug/;		# sort + as aug
    }
    $a0 <=> $b0 || $arest cmp $brest;
}
# Dump a textual list of chord definitions.
# Should be handled by the ChordPro backend?

sub list_chords {
    my ( $chords, $origin, $hdr ) = @_;
    fill_tables();
    my @s;
    if ( $hdr ) {
	my $t = "-" x (((@tuning - 1) * 4) + 1);
	substr( $t, (length($t)-7)/2, 7, "strings" );
	push( @s,
	      "# CHORD CHART",
	      "# Generated by ChordPro " . $App::Music::ChordPro::VERSION,
	      "# http://www.chordpro.org",
	      "#",
	      "#            " . ( " " x 35 ) . $t,
	      "#       Chord" . ( " " x 35 ) .
	      join("",
		   map { sprintf("%-4s", $_) }
		   @tuning ),
	    );
    }

    foreach my $chord ( @$chords ) {
	my $info;
	if ( eval{ $chord->{name} } ) {
	    $info = $chord;
	}
	elsif ( $origin eq "chord" ) {
	    push( @s, sprintf( "{%s: %s}", "chord", $chord ) );
	    next;
	}
	else {
	    $info = chord_info($chord);
	}
	next unless $info;
	my $s = sprintf( "{%s: %-15.15s base-fret %2d    ".
			 "frets   %s",
			 $origin eq "chord" ? "chord" : "define",
			 $info->{name}, $info->{base},
			 @{ $info->{strings} }
			 ? join("",
				map { sprintf("%-4s", $_) }
				map { $_ < 0 ? "X" : $_ }
				@{ $info->{strings} } )
			 : ("    " x strings() ));
	$s .= join("", "    fingers ",
		   map { sprintf("%-4s", $_) }
		   map { $_ < 0 ? "X" : $_ }
		   @{ $info->{fingers} } )
	  if $info->{fingers} && @{ $info->{fingers} };
	$s .= "}";
	push( @s, $s );
    }
    \@s;
}

sub dump_chords {
    my ( $mode ) = @_;
    fill_tables();
    print( join( "\n",
		 $mode && $mode == 2
		 ? @{ json_chords(\@chordnames ) }
		 : @{ list_chords(\@chordnames, "__CLI__", 1) } ), "\n" );
}

sub json_chords {
    my ( $chords ) = @_;
    fill_tables();
    my @s;

    push( @s, "// ChordPro instrument definition.",
	  "",
	  qq<{ "instrument" : "> .
	  ($::config->{instrument} || "Guitar, 6 strings, standard tuning") .
	  qq<",>,
	  "",
	  qq<  "tuning" : [ > .
	  join(", ", map { qq{"$_"} } @tuning) . " ],",
	  "",
	  qq{  "chords" : [},
	  "",
	 );

    my $maxl = -1;
    foreach my $chord ( @$chords ) {
	my $t = length( $chord );
	$maxl < $t and $maxl = $t;
    }
    $maxl += 2;

    foreach my $chord ( @$chords ) {
	my $info;
	if ( eval{ $chord->{name} } ) {
	    $info = $chord;
	}
	else {
	    $info = chord_info($chord);
	}
	next unless $info;

	my $name = '"' . $info->{name} . '"';
	my $s = sprintf( qq[    { "name" : %-${maxl}.${maxl}s,] .
                         qq[ "base" : %2d,],
			 $name, $info->{base} );
	if ( @{ $info->{strings} } ) {
	    $s .= qq{ "frets" : [ } .
	      join( ", ", map { sprintf("%2s", $_) } @{ $info->{strings} } ) .
		qq{ ],};
	}
	if ( $info->{fingers} && @{ $info->{fingers} } ) {
	    $s .= qq{ "fingers" : [ } .
	      join( ", ", map { sprintf("%2s", $_) } @{ $info->{fingers} } ) .
		qq{ ],};
	}
	chop($s);
	$s .= " },";
	push( @s, $s );
    }
    chop( $s[-1] );
    push( @s, "", "  ]," );
    if ( $::config->{pdf}->{diagrams}->{vcells} ) {
	push( @s, qq<  "pdf" : { "diagrams" : { "vcells" : > .
	      $::config->{pdf}->{diagrams}->{vcells} . qq< } },> );
    }
    chop( $s[-1] );
    push( @s, "}" );
    \@s;
}

################ Section Tuning ################

# Return the number of strings supported.
sub strings {
    scalar(@tuning);
}

sub set_tuning {
    my ( $t ) = @_;
    return "Invalid tuning (not array)" unless ref($t) eq "ARRAY";
    @tuning = @$t;		# need more checks
    fill_tables();
    @chordnames = ();
    %chords = ();
    %config_chords = ();
    return;

}

################ Section Config Chords ################

# Add a config defined chord.
sub add_config_chord {
    my ( $name, $base, $frets, $fingers ) = @_;
    # unless ( $frets ) {
    #	$frets = [(0) x strings()];
    #	$base = 0;
    # }
    unless ( @$frets == strings() ) {
	return scalar(@$frets) . " strings";
    }
    if ( $fingers && @$fingers && @$fingers != strings() ) {
	return scalar(@$frets) . " strings";
    }
    unless ( $base > 0 && $base < 24 ) {
	return "base-fret $base out of range";
    }
    $config_chords{$name} = [ !CHORD_SONG, $base, @$frets,
			      $fingers && @$fingers ? @$fingers : () ];
    push( @chordnames, $name );
    return;
}

################ Section User (Song) Chords ################

# Reset user defined songs. Should be done for each new song.
sub reset_song_chords {
    %song_chords = ();
}

# Add a user defined chord.
sub add_song_chord {
    my ( $name, $base, $frets, $fingers ) = @_;
    if ( @$frets != strings() ) {
	return scalar(@$frets) . " strings";
    }
    if ( $fingers && @$fingers && @$fingers != strings() ) {
	return scalar(@$fingers) . " strings for fingers";
    }
    unless ( $base > 0 && $base < 24 ) {
	return "base-fret $base out of range";
    }
    $song_chords{$name} = [ CHORD_SONG, $base, @$frets,
			    $fingers && @$fingers ? @$fingers : () ];
    return;
}

# Add an unknown chord.
sub add_unknown_chord {
    my ( $name ) = @_;
    my $base = 0;
    my $frets = [ (0) x strings() ];
    $song_chords{$name} = [ CHORD_SONG, $base, @$frets ];
    return +{
	     name    => $name,
	     strings => [ ],
	     base    => -1,
	     user    => 1,
	    };
}

################ Section Chords Parser ################

my $additions_maj =
  {
   map { $_ => $_ }
   "",
   "11",
   "13",
   "13#11",
   "13#9",
   "13b9",
   "13sus",
   "2",
   "5",
   "6",
   "69",
   "7",
   "7#11",
   "7#5",
   "7#9",
   "7#9#11",
   "7#9#5",
   "7#9b5",
   "7alt",
   "7b13",
   "7b13sus",
   "7b5",
   "7b9",
   "7b9#11",
   "7b9#5",
   "7b9#9",
   "7b9b13",
   "7b9b5",
   "7b9sus",
   "7sus",
   "7susadd3",
   "9",
   "9#11",
   "9#5",
   "9b5",
   "9sus",
   ( map { ( "maj$_", "^$_" ) }
     "",
     "13",
     "7",
     "7#11",
     "7#5",
     "9",
     "9#11",
   ),
   "add9",
   "alt",
   "h",
   "h7",
   "h9",
   ( map { "sus$_" } "", "2", "4", "9" ),
  };

my $additions_min =
  {
   map { $_ => $_ }
   "",
   "#5",
   "11",
   "6",
   "69",
   "7b5",
   ( map { ( "$_", "maj$_", "^$_" ) }
     "7",
     "9",
   ),
   "b6",
  };

my $additions_aug =
  {
   map { $_ => $_ }
   "",
  };

my $additions_dim =
  {
   map { $_ => $_ }
   "",
   "7",
  };

my %additions_map =
  ( ""  => $additions_maj,
    "-" => $additions_min,
    "+" => $additions_aug,
    "0" => $additions_dim,
  );

# Notes, sharp series ( C, C#, D, D#, ... )
my @notes_sharp = split( ' ', "C C# D D# E F F# G G# A A# B" );
# Notes, flat series ( C, Dd, D, Eb, ... )
my @notes_flat  = split( ' ', "C Db D Eb E F Gb G Ab A Bb B" );
# Notes -> canonical ( C = 0, D = 2, ... )
my %notes2canon; {
    my $ord = 0;
    for ( @notes_sharp ) {
	$notes2canon{$_} = $ord;
	$ord++;
    }
    $ord = 0;
    for ( @notes_flat ) {
	$notes2canon{$_} = $ord;
	$ord++;
    }
    $notes2canon{H} = $notes2canon{Bb};
}

my %tmap = ( C => 0, D => 2, E => 4, F => 5,
	     G => 7, A => 9, B => 11 );
my %nmap = ( 1 => 0, 2 => 2, 3 => 4, 4 => 5,
	     5 => 7, 6 => 9, 7 => 11 );
my @nmap = qw( I II III IV V VI VI );
my %rmap = ( I => 0, II => 2, III => 4, IV => 5,
	     V => 7, VI => 9, VII => 11 );
my $npat = qr{ ([b#]?) ([1-7]) }x;
my $rpat = qr{ ([b#]?) (IV|I{1,3}|VI{1,2}|V) }ix;
my $tpat = qr{ ( [CDEFGAB] )
	       ((?: [b#]
		    | (?<= [DGB]  ) es
		    | (?<= [EA]   ) s
		    | (?<= [CDFG] ) is
	       )?)
	 }x;

sub troot {
    $notes_sharp[$_[0]];
}

sub nroot {
    my $t = &troot;
    $t =~ s/(.)([#b])/$2$1/;
    $t =~ tr/CDEFGAB/1234567/;
    $t;
}

sub rroot {
    my $t = &nroot;
    $t =~ s/([1234567])/$nmap[$1-1]/e;
    $t;
}

my $ident_cache = {};

# Try to identify the argument as a valid chord.

sub identify {
    my ( $name ) = @_;
    return $ident_cache->{$name} if defined $ident_cache->{$name};

    my $rem = $name;
    my %info = ( name => $name, system => "" );

    # First some basic simplifications.
    $rem =~ tr/\x{266d}\x{266f}\x{0394}\x{f8}\x{b0}/b#^h0/;

    # Split off the duration, if present.
    if ( $rem =~ m;^(.*):(\d\.*)?(?:x(\d+))?$; ) {
	$rem = $1;
	$info{duration} = $2 // 1;
	$info{repeat} = $3;
    }

    # Split off the bass part, if present.
    my $bass = "";
    my $rootless;
    if ( $rem =~ m;^(.*)/(.*); ) {
	$bass = $2;
	$rem = $1;
	if ( $rem eq "" ) {
	    # Rootless. Fake a root by setting it to the bass.
	    # We'll remove the root info later.
	    $rootless++;
	    $rem = $bass;
	}
    }

    # Root processing.
    # Try: Traditional chord naming.
    if ( $rem =~ /^$tpat(.*)/ ) {

	$info{system} = "T";

	$info{dproot} = $1;
	my $root = $tmap{uc($1)};
	if ( $2 ) {
	    $root++ if $2 eq "#" || $2 eq "is";
	    $root-- if $2 eq "b" || $2 eq "es" || $1 eq "s";
	}
	$info{root} = $root % 12;
	$rem = $3;

	# Same for the bass.
	if ( $bass =~ m/^$tpat$/ ) {
	    $root = $tmap{uc($1)};
	    if ( $2 ) {
		$root++ if $2 eq "#" || $2 eq "is";
		$root-- if $2 eq "b" || $2 eq "es" || $1 eq "s";
	    }
	    $info{bass} = $root % 12;
	    $bass = "";
	}
    }

    # Try: Nashville Number System.
    elsif ( $rem =~ /^$npat(.*)/ ) {

	$info{system} = "N";

	$info{dproot} = $2;
	my $root = $nmap{$2};
	$root++ if $1 eq "#";
	$root-- if $1 eq "b";
	$info{root} = $root % 12;
	$rem = $3;

	# Same for the bass.
	if ( $bass =~ /^$npat$/ ) {
	    $root = $nmap{$2};
	    $root++ if $1 eq "#";
	    $root-- if $1 eq "b";
	    $info{bass} = $root % 12;
	    $bass = "";
	}
    }

    # Try: Roman Number System.
    elsif ( $rem =~ /^$rpat(.*)/ ) {

	$info{system} = "R";

	$info{dproot} = $2;
	my $root = $rmap{uc($2)};
	$root++ if $1 eq "#";
	$root-- if $1 eq "b";
	$info{root} = $root % 12;
	$info{qual} = $2 eq lc($2) ? "-" : ""; # implied by case
	$rem = $3;

	if ( $bass =~ m/^$rpat$/ ) {
	    $root = $rmap{uc($2)};
	    $root++ if $1 eq "#";
	    $root-- if $1 eq "b";
	    $info{bass} = $root % 12;
	    $bass = "";
	}
    }

    # Fallback to known chords. Maybe it is user defined.
    else {
	fill_tables();
	for ( \%song_chords, \%config_chords ) {
	    next unless exists($_->{$name});
	    $info{system} = "U";
	    return $ident_cache->{$name} = \%info;
	}

	# Final fallback to built-in chords.
	for ( \%chords ) {
	    next unless exists($_->{$name});
	    $info{system} = "B";
	    return $ident_cache->{$name} = \%info;
	}

	# Unknown/unparsable.
	$info{error} = $rem;
	$info{error} .= "/$bass" if $bass ne "";
	return $ident_cache->{$name} = \%info;
    }

    $info{nonroot} = $rem;

    # Chord quality, based on triads.
    $info{qual} //= "";
    if ( $rem =~ /^ ( aug | dim | min | m(?!aj) | [-+0] ) (.*) /x ) {
	$info{qual} = "+" if $1 eq "aug" || $1 eq "+";
	$info{qual} = "0" if $1 eq "dim" || $1 eq "0";
	$info{qual} = "-" if $1 eq "min" || $1 eq "-" || $1 eq "m";
	$rem = $2;
    }

    if ( $rem ne "") {
	$info{adds} = $rem;
	$rem = "";
	unless ( exists $additions_map{$info{qual}}{$rem} ) {
	    $info{warning} = "";
	    $info{warning} = $rem if $rem ne "";
	    $info{warning} .= "/$bass" if $bass ne "";
	}
    }

    # Did we process everything?
    if ( $rem ne "" || $bass ne "" ) {
	# Signal error.
	$info{error} = "";
	$info{error} = $rem if $rem ne "";
	$info{error} .= "/$bass" if $bass ne "";
    }
    # Remove fake root, if any.
    elsif ( $rootless ) {
	delete @info{ qw(root qual adds) };
    }

    return $ident_cache->{$name} = \%info;
}

################ Section Transposition ################

sub transpose {
    my ( $c, $xpose ) = @_;
    return $c unless $xpose;
    fill_tables();
    return $c unless $c =~ m/
				^ (
				    [CF](?:is|\#)? |
				    [DG](?:is|\#|es|b)? |
				    A(?:is|\#|s(?!us)|b)? |
				    E(?:s(?!us)|b)? |
				    B(?:es|b)? |
				    H
				  )
				  (.*)
			    /x;
    my ( $r, $rest ) = ( $1, $2 );
    my $mod = 0;
    $mod-- if $r =~ s/(e?s|b)$//;
    $mod++ if $r =~ s/(is|\#)$//;
    warn("WRONG NOTE: '$c' '$r' '$rest'") unless defined $notes2canon{$r};
    $r = ($notes2canon{$r} + $mod + $xpose) % 12;

    if ( $rest =~ m;^(.*)/(.*); ) {
	$rest = $1 . "/" . transpose( $2, $xpose );
    }

    return ( $xpose > 0 ? \@notes_sharp : \@notes_flat )->[$r] . $rest;
}


unless ( caller ) {
    $App::Music::ChordPro::VERSION = "";
    #    dump_chords();
    require DDumper;
    DDumper(identify($_)) foreach @ARGV;
#    DDumper( $additions_maj );
}

1;
